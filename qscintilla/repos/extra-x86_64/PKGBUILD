# $Id: PKGBUILD 46926 2009-07-22 04:56:17Z pierre $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Contributor: DaNiMoTh <jjdanimoth.aur@gmail.com>

pkgname=qscintilla
pkgver=2.4.4
pkgrel=1
license=('GPL')
pkgdesc="A port to Qt4 of Neil Hodgson's Scintilla C++ editor class"
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.co.uk/software/qscintilla/intro"
depends=('qt')
provides=('qscintilla2')
conflicts=('qscintilla2')
replaces=('qscintilla2')
source=("http://riverbankcomputing.com/static/Downloads/QScintilla2/QScintilla-gpl-${pkgver}.tar.gz")
md5sums=('db5f21723ea976ddd78c4c3662a21f9c')


build() {
    cd $srcdir/QScintilla-gpl-$pkgver/Qt4
    sed -i "s|header.path = .*|header.path = $pkgdir/usr/include|g" qscintilla.pro
    sed -i "s|trans.path = .*|trans.path = $pkgdir/usr/share/qt/translations|g" qscintilla.pro
    sed -i "s|qsci.path = .*|qsci.path = $pkgdir/usr/share/qt/translations|g" qscintilla.pro

    qmake qscintilla.pro
    sed -i 's|$(MOVE) $(TARGET)|$(COPY) $(TARGET)|g' Makefile
    make
    
    # Qt4 designer qscintilla2 plugin
    cd ../designer-Qt4
    install -d ${pkgdir}/usr/lib/qt/plugins/designer
    echo "INCPATH += ../Qt4" >> designer.pro
    sed -i "s|LIBS.*|LIBS += -L../Qt4 -lqscintilla2|g" designer.pro
    qmake designer.pro
    make
}

package() {
  cd ${srcdir}/QScintilla-gpl-${pkgver}/Qt4
  make DESTDIR="${pkgdir}" install
  cp libqscintilla2.so.5.4.0 ${pkgdir}/usr/lib/
  ln -s libqscintilla2.so.5.4.0 ${pkgdir}/usr/lib/libqscintilla2.so
  ln -s libqscintilla2.so.5.4.0 ${pkgdir}/usr/lib/libqscintilla2.so.5
  ln -s libqscintilla2.so.5.4.0 ${pkgdir}/usr/lib/libqscintilla2.so.5.4

  cd ${srcdir}/QScintilla-gpl-${pkgver}/designer-Qt4
  cp libqscintillaplugin.so ${pkgdir}/usr/lib/qt/plugins/designer/
}
