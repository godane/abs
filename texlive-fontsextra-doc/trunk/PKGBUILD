# Maintainer: Firmicus <firmicus āt gmx dōt net>

pkgname=texlive-fontsextra-doc
pkgver=2008.13014
pkgrel=1
pkgdesc="TeX Live - all sorts of extra fonts"
license=('GPL')
arch=(any)
depends=('texlive-fontsextra')
makedepends=('lzma-utils')
groups=('texlive-most-doc')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-src.zip"
${pkgname}_13014.pkgs)
md5sums=('36d83b6f8a859695ccb66043e7e22a27' '353582cb9acec2e8f794bf9b6e92b2b1')
options=('!emptydirs')
install=texlive.install

package() {
   cd $srcdir
   for p in *.tar.lzma; do
           lzma --force -dc $p | tar xf -
   done
   install -m755 -d $pkgdir/usr/share
   find texmf* -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
   find texmf* -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   install -m755 -d $pkgdir/usr/share/texmf-var/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS \
     $pkgdir/usr/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
}
