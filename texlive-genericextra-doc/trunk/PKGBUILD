# Maintainer:Stefan Husmann <stefan-husmann@t-online.de>

pkgname=texlive-genericextra-doc
pkgver=2008.12667
pkgrel=1
pkgdesc="TeX Live - mixed bag of generic macro packages and fonts"
license=('GPL')
arch=(any)
depends=('texlive-genericextra')
makedepends=('lzma-utils')
groups=('texlive-most-doc')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-src.zip" \
${pkgname}_12667.pkgs)
md5sums=('2ccb25866a0fe87cfa9a700d71c5365a' '2d04bd262af93bf849dd15e0d9a8c3cf')
install=texlive.install

package() {
   cd $srcdir
   for p in *.tar.lzma; do
           lzma --force -d $p
           tar xf  ${p%.lzma}
   done
   install -m755 -d $pkgdir/usr/share
   find texmf* -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
   find texmf* -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   install -m755 -d $pkgdir/usr/share/texmf-var/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS \
     $pkgdir/usr/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
}
