# $Id$
# Maintainer:
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=5.10.0
pkgrel=1
pkgdesc="Prolog environment"
arch=('i686' 'x86_64')
url="http://www.swi-prolog.org/"
license=('GPL' 'LGPL')
depends=('gmp' 'readline' 'openssl')
makedepends=('libxft' 'libjpeg' 'unixodbc' 'libxpm' 'libxinerama')
optdepends=('unixodbc: for using the odbc4pl library' \
            'libjpeg: for using the pl2xpce library' \
            'libxpm: for using the pl2xpce library' \
            'libxinerama: for using the pl2xpce library' \
            'libxft: for using the pl2xpce library')
options=('!makeflags')
source=(http://www.swi-prolog.org/download/stable/src/pl-${pkgver}.tar.gz)
md5sums=('ebff502a0faa40e584255b6ddb163da9')
sha1sums=('0115ec399b59848879e9053fbefeec209477f7c7')

build() {
  cd "${srcdir}/pl-${pkgver}"
  ./configure --prefix=/usr --with-world
  make
}

package() {
  cd "${srcdir}/pl-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
