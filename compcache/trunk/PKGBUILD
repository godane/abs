# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Tibor Bamhor <tiborb95 at gmail dot com>

pkgname=compcache
pkgver=0.6.2
pkgrel=8
pkgdesc="Compressed in-memory swap device"
url="http://code.google.com/p/compcache/"
license=(GPL)
arch=('i686' 'x86_64')
backup=(etc/conf.d/compcache)
depends=('kernel26>=2.6.33')
source=(http://compcache.googlecode.com/files/compcache-$pkgver.tar.gz
        compcache.conf compcache.init)
md5sums=('27aec78dc50e34fb800c74e879057743'
         'f980ceaa28242d4ce7dd9df919a940f7'
         '34017b090295ad2f6a129af75b7d7b74')

build() {
  cd "$srcdir/$pkgname-$pkgver/sub-projects/rzscontrol"

  make all || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # rzscontrol
  install -Dm 755 sub-projects/rzscontrol/rzscontrol "$pkgdir/usr/sbin/rzscontrol" || return 1
  install -Dm 644 sub-projects/rzscontrol/man/rzscontrol.1 "$pkgdir/usr/share/man/man1/rzscontrol.1" || return 1

  # Documentation
  install -Dm 644 README "$pkgdir/usr/share/doc/$pkgname/README" || return 1

  # Initscript
  install -Dm 755 "$srcdir/compcache.init" "$pkgdir/etc/rc.d/compcache" || return 1
  install -Dm 644 "$srcdir/compcache.conf" "$pkgdir/etc/conf.d/compcache" || return 1
}

