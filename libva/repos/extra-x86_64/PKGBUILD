# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
pkgname=libva
pkgver=0.31.1_p3
pkgrel=1
pkgdesc="libva with patches to extend VA API with data needed for VDPAU and XvBA backends."
arch=('i686' 'x86_64')
url="http://www.splitted-desktop.com/~gbeauchesne/libva/"
license=('MIT')
depends=('libgl' 'libdrm' 'libxfixes')
makedepends=('mesa')
optdepends=('vdpau-video: libvdpau support')
options=('!libtool')
source=(http://www.splitted-desktop.com/~gbeauchesne/${pkgname}/${pkgname}_${pkgver/_p*}-1+sds${pkgver##*_p}.tar.gz)
md5sums=('f84b5e128a45ac953afa244a733f1611')

build() {
  cd ${srcdir}/${pkgname}-${pkgver/_p*}
  for p in debian/patches/*.patch;
    do patch -p1 < $p
  done
  autoreconf -fi
  ./configure --prefix=/usr  --enable-i965-driver

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver/_p*}
  make DESTDIR="${pkgdir}" install
  install -m644 -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
