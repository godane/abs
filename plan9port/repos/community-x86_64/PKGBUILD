# $Id$
# Contributor: phrakture <aaronmgriffin--gmail--com>
# Contributor: Fazlul Shahriar
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
pkgname=plan9port
pkgver=20100520
pkgrel=1
pkgdesc="A port of many programs from Plan 9 to Unix-like operating systems"
arch=('i686' 'x86_64')
url="http://swtch.com/plan9port/"
license=('custom')
depends=(xorg-server sh fuse)
install=plan9.install
source=(http://swtch.com/$pkgname/$pkgname-$pkgver.tgz plan9.sh)
md5sums=('c563eedcb6a24848c711901b8dbd1141'
	'9695eb15a247e23140e8a35ff52a5c20')

build()
{
  install -Dm755 "$srcdir/plan9.sh" "$pkgdir/etc/profile.d/plan9.sh" || return 1

  install -dm755 "$pkgdir/opt" "$pkgdir/usr/share/doc/$pkgname" || return 1
  mv "$srcdir/plan9" "$pkgdir/opt"

  cd "$pkgdir/opt/plan9"
  ./INSTALL || return 1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1

  # Don't need these under /opt/plan9.
  rm -rf .hg/
  rm -f .hgignore .hgtags
  find . -name '.cvsignore' -print0 |xargs -0 rm -f
  rm -f config install.log install.sum install.txt configure Makefile INSTALL \
    LICENSE

  borkedfiles=`grep -r "$pkgdir"/opt/plan9 * | grep -v Binary | cut -d\: -f1`
  for f in $borkedfiles; do
    echo "fixing hardcoded paths in $f"
    sed -i "s@$pkgdir/opt/plan9@/opt/plan9@" $f || return 1
  done

  for i in CHANGES CONTRIBUTORS README TODO
    do
    install -m644 $i "$pkgdir/usr/share/doc/$pkgname" || return 1
    rm -f $i
  done
}
