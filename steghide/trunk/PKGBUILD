# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jason Chu <jason@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=steghide
pkgver=0.5.1
pkgrel=4
pkgdesc="A program which embeds a message in a file by replacing some of the least significant bits"
arch=('i686' 'x86_64')
url="http://steghide.sourceforge.net"
license=('GPL')
depends=('libmcrypt' 'mhash' 'gcc' 'libjpeg' 'zlib' 'libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz \
	buildsystem.patch gcc-4.2.patch ${pkgname}-climits.patch)
md5sums=('5be490e24807d921045780fd8cc446b3'
         '14f5e054d71c91262bb1140288043df8'
         'fd861dc5a0a392c6c94a61e85ad74f29'
         '897ce18edd008293a3328bc3f88775b3')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -p1 < ../buildsystem.patch || return 1
	# this patch courtesy gentoo bug tracker #178134
	patch -Np1 -i ../gcc-4.2.patch || return 1
	patch -Np1 -i ../steghide-climits.patch || return 1
	autoreconf -i
	./configure --prefix=/usr
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install || return 1
}
