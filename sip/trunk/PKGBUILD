# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgname=sip
pkgver=4.10.3
pkgrel=1
pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
depends=('python' 'gcc-libs')
source=("http://riverbankcomputing.com/static/Downloads/${pkgname}4/${pkgname}-${pkgver}.tar.gz")
md5sums=('cb0922b5d12add2d36061e43be64f5ce')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  python configure.py -b /usr/bin \
    -d /usr/lib/python2.6/site-packages \
    -e /usr/include/python2.6 \
    -v /usr/share/sip

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
