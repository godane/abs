# $Id: PKGBUILD 75257 2010-04-01 05:30:58Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

pkgname=python2
pkgver=2.7
pkgrel=1
_pybasever=2.7
pkgdesc="A high-level scripting language"
arch=('i686' 'x86_64')
license=('PSF')
url="http://www.python.org"
depends=('db' 'bzip2' 'gdbm' 'openssl' 'zlib' 'expat' 'sqlite3')
makedepends=('tk')
optdepends=('tk: for IDLE')
conflicts=('python<3')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2)
md5sums=('0e8c9ec32abf5b732bea7d91b38c3339')

build() {
  cd ${srcdir}/Python-${pkgver}

  export OPT="${CFLAGS}"
  ./configure --prefix=/usr --enable-shared --with-threads \
              --enable-unicode=ucs4 --with-system-expat

  make
}

package() {
  cd ${srcdir}/Python-${pkgver}
  make DESTDIR=${pkgdir} altinstall

  ln -sf python${_pybasever} $pkgdir/usr/bin/python2
  ln -sf python${_pybasever}-config $pkgdir/usr/bin/python2-config

  ln -sf ../../libpython${_pybasever}.so \
    ${pkgdir}/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so

  mv $pkgdir/usr/bin/smtpd.py $pkgdir/usr/lib/python${_pybasever}/

  # clean-up reference to build directory
  sed -i "s#$srcdir/Python-${pkgver}:##" \
    $pkgdir/usr/lib/python${_pybasever}/config/Makefile

  # some useful "stuff"
  install -dm755 ${pkgdir}/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    ${pkgdir}/usr/lib/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} \
    ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/

  # fix conflicts with python
  mv $pkgdir/usr/bin/idle{,2}
  mv $pkgdir/usr/bin/pydoc{,2}

  # license
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
