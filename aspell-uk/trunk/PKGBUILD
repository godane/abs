# $Id$
# Maintainer : Roman Kyrylych <roman@archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=aspell-uk
pkgver=1.6.0
_ftpver=1.4.0
pkgrel=1
pkgdesc="Ukrainian dictionary for aspell"
arch=('i686' 'x86_64')
url="http://ispell-uk.sourceforge.net/"
license=('GPL' 'LGPL' 'MPL')
depends=('aspell')
source=(ftp://ftp.gnu.org/gnu/aspell/dict/uk/aspell6-uk-${_ftpver}-0.tar.bz2
        http://downloads.sourceforge.net/ispell-uk/spell-uk-$pkgver.tgz)
md5sums=('662f15381d11581758866fd7af43b4d7'
         '4177ed3e196e6511513cf358c4a42e1a')

build() {
  # needed when using a source package from ispell-uk project
  cd ${srcdir}/spell-uk-$pkgver
  make

  cd ${srcdir}/aspell6-uk-${_ftpver}-0
  ./configure
  make

  # needed when using a source package from ispell-uk project
  mv ${srcdir}/spell-uk-$pkgver/dist/aspell-uk-$pkgver/* \
    ${srcdir}/aspell6-uk-${_ftpver}-0
}

package() {
  cd ${srcdir}/aspell6-uk-${_ftpver}-0

  make DESTDIR="$pkgdir" install
}
