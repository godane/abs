# $Id: PKGBUILD 85346 2010-07-12 17:26:03Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gvfs
pkgname=('gvfs' 'gvfs-smb' 'gvfs-afc' 'gvfs-gphoto2' 'gvfs-obexftp')
pkgver=1.6.3
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('libsoup-gnome>=2.30.2' 'libcdio>=0.82' 'fuse>=2.8.4' 'bluez>=4.67' 'smbclient>=3.5.4' 'libgphoto2>=2.4.9' 'libarchive>=2.8.4' 'gnome-disk-utility>=2.30.1' 'pkgconfig' 'intltool' 'libimobiledevice>=1.0.2')
url="http://www.gnome.org"
options=(!libtool)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/1.6/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('4e3fe2dd80e5baaf4c1844560f6a787b144a917de1cb457b41efcb338477e760')


build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gvfs \
      --with-bash-completion-dir=/etc/bash_completion.d
  make
}

package_gvfs() {
  pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio"
  depends=('libsoup-gnome>=2.30.2' 'gnome-disk-utility>=2.30.1' 'libcdio>=0.82' 'fuse>=2.8.4' 'libarchive>=2.8.3')
  optdepends=('gvfs-afc: AFC (mobile devices) support'
              'gvfs-smb: SMB/CIFS (Windows client) support'
              'gvfs-gphoto2: gphoto2 (PTP camera/MTP media player) support'
              'gvfs-obexftp: ObexFTP (bluetooth) support')
  install=gvfs.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -e 's/^am__append_3/#am__append_3/' \
      -e 's/^am__append_4/#am__append_4/' \
      -i monitor/Makefile
  make DESTDIR="${pkgdir}" install
  cd "${pkgdir}"
  rm -f usr/lib/gvfs/gvfsd-{smb,smb-browse,afc,gphoto2,obexftp}
  rm -f usr/share/gvfs/mounts/{smb,smb-browse,afc,gphoto2,obexftp}.mount
}

package_gvfs-smb() {
  pkgdesc="SMB/CIFS (Windows client) backend for gvfs"
  depends=("gvfs=${pkgver}" 'smbclient>=3.5.4' 'libgnome-keyring>=2.30.1')
  install=gvfs-module.install

  cd "${srcdir}/${pkgbase}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-smb{,-browse} "${pkgdir}/usr/lib/gvfs/"
  install -m644 smb{,-browse}.mount "${pkgdir}/usr/share/gvfs/mounts/"
}

package_gvfs-afc() {
  pkgdesc="AFC (mobile devices) backend for gvfs"
  depends=("gvfs=${pkgver}" 'libimobiledevice>=1.0.2')
  install=gvfs-module.install

  cd "${srcdir}/${pkgbase}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-afc "${pkgdir}/usr/lib/gvfs/"
  install -m644 afc.mount "${pkgdir}/usr/share/gvfs/mounts/"

  cd "${srcdir}/${pkgbase}-${pkgver}/monitor/afc"
  make DESTDIR="${pkgdir}" install
}

package_gvfs-gphoto2() {
  pkgdesc="gphoto2 (PTP camera/MTP media player) backend for gvfs"
  depends=("gvfs=${pkgver}" 'dbus-core>=1.2.24' 'libgphoto2>=2.4.9' 'udev>=159')
  install=gvfs-module.install

  cd "${srcdir}/${pkgbase}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-gphoto2 "${pkgdir}/usr/lib/gvfs/"
  install -m644 gphoto2.mount "${pkgdir}/usr/share/gvfs/mounts/"

  cd "${srcdir}/${pkgbase}-${pkgver}/monitor/gphoto2"
  make DESTDIR="${pkgdir}" install
}

package_gvfs-obexftp() {
  pkgdesc="ObexFTP (bluetooth) backend for gvfs"
  depends=("gvfs=${pkgver}" 'dbus-glib>=0.86' 'bluez>=4.67' 'obex-data-server')
  install=gvfs-module.install

  cd "${srcdir}/${pkgbase}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-obexftp "${pkgdir}/usr/lib/gvfs/"
  install -m644 obexftp.mount "${pkgdir}/usr/share/gvfs/mounts/"
}
