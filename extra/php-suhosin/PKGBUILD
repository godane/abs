# $Id: PKGBUILD 86069 2010-07-25 11:35:16Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-suhosin
pkgver=0.9.32.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='An advanced protection system for PHP installations'
url='http://www.hardened-php.net/suhosin/'
license='PHP'
source=("http://download.suhosin.org/suhosin-${pkgver}.tar.gz")
depends=('php')
conflicts=('php-suhosin-extension')
replaces=('php-suhosin-extension')
backup=('etc/php/conf.d/suhosin.ini')
md5sums=('26a86f0f684a656c3e789e3eb4ec1db3')

build() {
	cd ${srcdir}/suhosin-${pkgver}
	phpize
	./configure --prefix=/usr --enable-suhosin
	make
}

package() {
	cd ${srcdir}/suhosin-${pkgver}
	make INSTALL_ROOT=${pkgdir} install
	# disable by default
	sed -i 's|extension = suhosin.so|;extension=suhosin.so|g' suhosin.ini
	install -D -m644 suhosin.ini ${pkgdir}/etc/php/conf.d/suhosin.ini
}
