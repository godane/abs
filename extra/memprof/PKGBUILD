# $Id$
# Maintainer: aurelien <aurelien@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=memprof
pkgver=0.6.2
pkgrel=1
pkgdesc="A tool for profiling memory usage and finding memory leaks"
arch=('i686')
url="http://www.secretlabs.de/projects/memprof/"
license=('GPL')
depends=('libgnomeui')
makedepends=('pkg-config' 'perl-xml-parser' 'intltool')
options=('!libtool')
install=memprof.install
source=(http://ftp.gnome.org/pub/GNOME/sources/memprof/0.6/${pkgname}-${pkgver}.tar.gz)
md5sums=('48b549139a06bd1bf89374a19890f087')
sha1sums=('580feffc62cfe700d1b25667d4364df5c348f859')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
   
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
