# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=foobillard
pkgver=3.0a
pkgrel=6
pkgdesc="An OpenGL billiard game for Linux"
arch=('i686' 'x86_64')
url="http://foobillard.sourceforge.net/"
license=('GPL2' 'custom')
depends=('sdl' 'freetype2' 'libpng' 'mesa')
changelog=ChangeLog
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        foobillard.desktop foobillard.png 03_bugfixes 05_foul_explanation \
        06_show_ball_to_hit snooker_reset.patch snooker_draw.patch)
md5sums=('c2d92edeaaf8bfb18aa26f1c79931b7d' '436f76ad44910a2524a38ff158bc32fc'\
         '4bb47566197a252388bf49583536dd4d' '4400c87674b8a442c0cbbd619e118cd3'\
         '0b79b9b71bebd7e0e1584f0593cc348d' 'eed13569ab099054619137eabce96045'\
         '329ddc735af17f33df99c5fe7ff672b9' '97f04be3ad4a0f78c6d7a8b658099a5d')
sha1sums=('1caa51a201b55883640b5c5b1e9fde20fc71a095' '7e820e047497eae5f3d495cc8c61870d7b438a2e'\
         '67db7b2a96784ff3baaeca9610414376210e5c49' '119aa2bfcfb2b074e0d9b441441808c22eacf625'\
         '347535e3db4b04caab2f040779576229b2ce71ac' '11cff3e62fccd101cc85d58cc0d0eb4a1b99670f'\
         '405a64db7e92e7ba046dd9007d4bf1433914ef79' 'a3151458667488117524ce8948b0d101657ad425')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/-lXaw//' -e 's/-lXi//' src/Makefile.in
  patch -p1 < ../03_bugfixes
  patch -p1 < ../05_foul_explanation
  patch -p1 < ../06_show_ball_to_hit
  patch -p0 < ../snooker_reset.patch
  patch -p0 < ../snooker_draw.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 foobillard.6 "${pkgdir}/usr/share/man/man6/foobillard.6"
  install -D -m644 README.FONTS "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.FONTS"
  install -D -m644 ../foobillard.desktop "${pkgdir}/usr/share/applications/foobillard.desktop"
  install -D -m644 ../foobillard.png "${pkgdir}/usr/share/pixmaps/foobillard.png"
}
