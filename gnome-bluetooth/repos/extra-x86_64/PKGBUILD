# $Id$
# Maintainer:
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=2.30.0
pkgrel=3
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('libunique>=1.1.6' 'libnotify>=0.4.5' 'gconf>=2.28.1' 'hicolor-icon-theme' 'gvfs-obexftp>=1.6.3' 'obexd-client>=0.29')
makedepends=('intltool' 'pygtk>=2.17' 'gnome-doc-utils>=0.20.1' 'nautilus-sendto>=2.28.4' 'gobject-introspection>=0.6.14' 'gir-repository')
replaces=('bluez-gnome')
conflicts=('bluez-gnome')
options=('!libtool' '!emptydirs' '!makeflags')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        61-gnome-bluetooth-rfkill.rules)
sha256sums=('4caafa8c28195c81d8c51414f3a64489f81b9b46cac56979aeaaff8c9ebb78d4'
            'b8acb8ea2e7f3588575cffd8ea14ec50c8641f518f2ea899771a508b299ea474')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
    --domain gnome-bluetooth2 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  install -m644 -D "${srcdir}/61-gnome-bluetooth-rfkill.rules" \
                     "${pkgdir}/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
