# $Id$
# Maintainer:
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=2.30.0
pkgrel=3
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('libunique' 'libnotify' 'gconf' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client')
makedepends=('intltool' 'pygtk' 'gnome-doc-utils' 'nautilus-sendto' 'gobject-introspection' 'gir-repository')
replaces=('bluez-gnome')
conflicts=('bluez-gnome')
options=('!libtool' '!emptydirs' '!makeflags')
install=gnome-bluetooth.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2"
        61-gnome-bluetooth-rfkill.rules)
md5sums=('eb88212922037eb9751f0fcedb9c166e'
         '9fb6c8dcb5a3de3a953bf2ef42063e6e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
    --domain gnome-bluetooth2 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
  install -m644 -D "${srcdir}/61-gnome-bluetooth-rfkill.rules" \
                     "${pkgdir}/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
