# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=avifile
pkgver=0.7.45
pkgrel=7
pkgdesc="A library that allows you to read and write compressed AVI files"
arch=('i686' 'x86_64')
depends=('qt' 'sdl' 'freetype2' 'libpng' 'libjpeg' \
         'libvorbis' 'xvidcore' 'libxxf86dga' 'libxi' 'libxv')
makedepends=('lame' 'qt3')
url="http://avifile.sourceforge.net"
license=('GPL')
source=("http://downloads.sourceforge.net/sourceforge/avifile/$pkgname-0.7-$pkgver.tar.bz2"
	'gcc.patch')
md5sums=('7da94802f120d1b69e04a13170dcd21d'
         '588c3ce7bdebc1244f46ba56d8ef8a42')

build() {
  cd $pkgname-0.7-$pkgver

  patch -Np1 <../gcc.patch

 ./configure \
    --prefix=/usr \
    --mandir=$pkgdir/usr/share/man \
    --with-qt-prefix=/opt/qt \
    --with-qt-includes=/opt/qt/include \
    --with-qt-libraries=/opt/qt/lib \
    --enable-oss

  make
}

package() {
  cd $pkgname-0.7-$pkgver

  make prefix=$pkgdir/usr install

  find $pkgdir -name '*.la' -exec rm {} \;
}
