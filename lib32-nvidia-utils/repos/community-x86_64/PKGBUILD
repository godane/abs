# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer : Ionut Biru <ibiru@archlinux.org>

_pkgsourcename=nvidia-utils
pkgname=lib32-$_pkgsourcename
pkgver=256.35
pkgrel=5
pkgdesc="NVIDIA drivers utilities and libraries."
arch=('x86_64')
url="http://www.nvidia.com/"
license=('custom')
groups=('lib32')
depends=('lib32-libxext')
conflicts=('lib32-libgl')
provides=('lib32-libgl')
options=('!strip' '!libtool')
source=(ftp://download.nvidia.com/XFree86/Linux-x86_64/$pkgver/NVIDIA-Linux-x86_64-$pkgver.run)
md5sums=('7bcc2fcaec419c391d39a3b96492a6f1')

build() {
    cd $srcdir
    sh NVIDIA-Linux-x86_64-$pkgver.run --extract-only
}

package() {
    cd $srcdir
    mkdir -p $pkgdir/opt/lib32/usr/
    cp -dpr NVIDIA-Linux-x86_64-$pkgver/32 $pkgdir/opt/lib32/usr/lib
    for _lib in $(find $pkgdir -name '*.so*'); do
        _soname="$(dirname ${_lib})/$(readelf -d "$_lib" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
        if [ ! -e "${_soname}" ]; then
            ln -s "$(basename ${_lib})" "${_soname}"
            ln -s "$(basename ${_soname})" "${_soname/.[0-9]*/}"
        fi
    done

    install -d -m755 $pkgdir/usr/share/licenses/
    ln -s nvidia-utils $pkgdir/usr/share/licenses/$pkgname
}
