# Maintainer: Firmicus <firmicus ατ gmx δοτ net>

pkgname=texlive-core-doc
pkgver=2008.13013
pkgrel=1
pkgdesc="TeX Live core distribution"
license=('GPL')
arch=(any)
depends=('texlive-core')
makedepends=('lzma-utils')
groups=('texlive-most-doc')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-src.zip" ${pkgname}_13013.pkgs)
md5sums=('57653593b135c847d994c9ea9a560c53' '19656aefc939d29fb2a696e4992fffd9')
install=texlive.install

build() {
   cd $srcdir
   rm texlive-common.doc.tar.lzma
   for p in *.tar.lzma; do
           lzma --force -d $p
           tar xf ${p%.lzma}
   done
   install -m755 -d $pkgdir/usr/share
   find texmf* -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
   find texmf* -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   install -m755 -d $pkgdir/usr/share/texmf-var/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS \
     $pkgdir/usr/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
}
