# $Id$
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=metakit
pkgver=2.4.9.7
pkgrel=3
pkgdesc='MetaKit is an efficient database library with a small footprint'
arch=('i686' 'x86_64')
url='http://www.equi4.com/metakit/'
makedepends=('tcl' 'python')
license=('BSD')
options=('!libtool')
source=("http://www.equi4.com/pub/mk/${pkgname}-${pkgver}.tar.gz")
md5sums=('17330257376eea657827ed632ea62c9e')

build() {
  cd ${pkgname}-${pkgver}/builds

  ../unix/configure \
    --prefix=/usr \
    --with-tcl=/usr/include 

  make
}

package() {
  cd ${pkgname}-${pkgver}/builds

  mkdir -p ${pkgdir}/usr/lib/python2.6/site-packages

  make DESTDIR=${pkgdir} install

  install -D -m644 ../license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
