# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: damir <damir@archlinux.org>

pkgname=gpsdrive
pkgver=2.11
pkgrel=2
pkgdesc="A car (bike, ship, plane) navigation system"
arch=("i686" "x86_64")
url="http://www.gpsdrive.de/"
license=('GPL2')
depends=('gtk2' 'gpsd' 'libxml2' 'curl' 'python' 'boost-libs' 'gdal' 'mapnik' 'postgresql-libs' 'openstreetmap-map-icons-svn'
         'perl-date-manip' 'perl-timedate' 'perl-dbi' 'perl-file-slurp' 'perl-www-mechanize' 'perl-libwww' 'perl-uri'
         'perl-text-query' 'perl-www-curl' 'perl-xml-parser' 'perl-xml-simple' 'perl-xml-twig' 'perl-xml-writer'
        ) # already in core ('sqlite3')
makedepends=('cmake>=2.4.4' 'boost')
optdepends=()
options=()
install="gpsdrive.install"
source=("http://www.gpsdrive.de/packages/${pkgname}-${pkgver}.tar.gz")
md5sums=('6eeeca8e5c647115bea836d1f8fb6e0c')

build() {
  cd "$srcdir"

  rm -rf build
  mkdir build
  cd build

  # fix the mapnik default values
  sed -i 's|"/usr/lib/mapnik/0.7/input/"|"/usr/lib/mapnik/input/"|' "${srcdir}/gpsdrive-${pkgver}/src/gpsdrive_config.c"
  sed -i 's|"/usr/share/fonts/truetype/ttf-dejavu/"|"/usr/share/fonts/TTF/"|' "${srcdir}/gpsdrive-${pkgver}/src/gpsdrive_config.c"

  # see DefineOptions.cmake for a list of common options and defaults
  # cmake -L for a more in-depth listing
  cmake -DCMAKE_BUILD_TYPE=Debug -DCMAKE_INSTALL_PREFIX="/usr" \
    -DWITH_SCRIPTS=ON \
    -DWITH_MAPNIK=ON \
    -DWITH_POSTGIS=ON \
    -DWITH_GDAL=ON \
    -DWITH_BASEMAPS=ON \
    -DWITH_FRIENDSD=ON \
    -DWITH_KISMET=ON \
    -DWITH_NAVIGATION=ON \
    -DWITH_SPEECH=OFF \
    -DWITH_DBUS=OFF \
    -DLIBGPS_OLD=OFF \
    "$srcdir/gpsdrive-$pkgver" || return 1
  make || return 1
  make DESTDIR="$pkgdir" install
}
