# Maintainer: Xyne
# Contributor: Rick Chen (stuffcorpse)
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Camille Moncelier <pix@devlife.org>

pkgbase=nexuiz
pkgname=('nexuiz' 'nexuiz-data')
pkgver=2.5.2
_zipver=252
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.nexuiz.com/"
license=("GPL")
makedepends=('mesa')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_zipver}.zip \
        nexuiz-dedicated nexuiz-glx nexuiz-sdl nexuiz-glx.desktop \
        nexuiz-sdl.desktop nexuiz.png libpng14.patch)
md5sums=('d750bc328e58df8492f8d88bdcf818cb'
         '19bfaa6e891d3309783e8366db485474'
         '4d0ee9282a6fe153f82f7797decfd585'
         '3e5ea741e8d55df8e7691c797c81ffa7'
         '310f0577c39391f2d2f17cea446ce269'
         '4561d3c5be4801399af515faf82ebcba'
         '442fb62670bbe0a1b5370461052051a3'
	 '60964348924ff777a92dbb171a86a1ed')

build() {
  _enginesource_date="20091001"
  mkdir -p "$_nexdir"
  
  cd $srcdir/Nexuiz
  bsdtar -x -f sources/enginesource${_enginesource_date}.zip
  cd darkplaces
  patch -Np1 -i ${srcdir}/libpng14.patch
  # patch -p1 < "$srcdir/nexuiz-libjpeg-fix.patch"
  
  # here's a working kludge to build all three binaries at once but don't use it ;)
  # make CPUOPTIMIZATIONS="${CFLAGS}" nexuiz || make CPUOPTIMIZATIONS="${CFLAGS}" nexuiz
  
  # build the binaries separately instead to avoid truncated files
  make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 cl-nexuiz
  make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 sdl-nexuiz
  make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 sv-nexuiz

}

package_nexuiz() {
  pkgdesc="a free, open-source first person shooter"
  depends=('alsa-lib' 'curl' 'libjpeg>=8' 'libmodplug' 'libvorbis' 'libxpm' 'libxxf86dga' 'libxxf86vm' 'sdl' 'nexuiz-data' 'libpng>=1.4.0')
  cd $srcdir/Nexuiz/darkplaces
  _nexdir="$pkgdir/opt/nexuiz"
  mkdir -p "$_nexdir"

  # install the compiled binaries
  install -Dm755 nexuiz-glx nexuiz-sdl nexuiz-dedicated -t $_nexdir

  install -dm755 $pkgdir/usr/{bin,share/applications}
  install -Dm755 $srcdir/nexuiz-{glx,sdl,dedicated} -t $pkgdir/usr/bin
  install -Dm644 $srcdir/*.desktop -t $pkgdir/usr/share/applications
  install -Dm644 $srcdir/nexuiz.png $pkgdir/usr/share/pixmaps/nexuiz.png
}

package_nexuiz-data() {
  arch=('any')
  pkgdesc="Nexuiz game data"
  conflicts=('nexuiz<2.5.1-3')
  _nexdir="$pkgdir/opt/nexuiz"
  mkdir -p "$_nexdir"

  _install_dir $srcdir/Nexuiz/Docs $_nexdir/docs
  _install_dir $srcdir/Nexuiz/data $_nexdir/data
  _install_dir $srcdir/Nexuiz/havoc $_nexdir/havoc
  _install_dir $srcdir/Nexuiz/server $_nexdir/server
  
  find $_nexdir/server -name "*_windows.bat" -exec rm {} \;
  find $_nexdir/server -name "*_mac.sh" -exec rm {} \;
}

function _install_dir {
  _src_dir=$1
  _dest_dir=$2
  _n=${#_src_dir}
  for _file in $(find $_src_dir -type f)
  do
    _dest_file=${_dest_dir}${_file:$_n}
    install -Dm644 $_file $_dest_file
  done
}
