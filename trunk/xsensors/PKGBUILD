# $Id$
# Maintainer:
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=xsensors
pkgver=0.70
pkgrel=1
pkgdesc="X11 interface to lm_sensors"
arch=('i686' 'x86_64')
url="http://www.linuxhardware.org/xsensors/"
license=('GPL')
depends=('gtk2' 'lm_sensors')
changelog=$pkgname.changelog
source=(http://www.linuxhardware.org/xsensors/$pkgname-$pkgver.tar.gz
	fix-deprecated-GTK_WIDGET_STATE-call.diff)
md5sums=('4f8fb83cfd03c0cc34967a73c6021531'
         'e5ac8e58f0dd27501234692587c01b76')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "${srcdir}/fix-deprecated-GTK_WIDGET_STATE-call.diff"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 pixmaps/default.xpm \
    "$pkgdir/usr/share/$pkgname/pixmaps/default.xpm"
}
