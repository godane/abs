# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Hilton Medeiros <medeiros.hilton AT gmail DOT com>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Paolo Fagni <paolo.fagni AT mail DOT com>
# Contributor: Lukas Kropatschek <lukas.krop AT gmail DOT com>

pkgname=irrlicht
pkgver=1.7.1
pkgrel=1
pkgdesc="An open source high performance realtime 3D graphics engine."
arch=('i686' 'x86_64')
url="http://irrlicht.sourceforge.net/"
license=('ZLIB')
depends=('mesa' 'libjpeg' 'bzip2')
source=("http://downloads.sourceforge.net/irrlicht/$pkgname-$pkgver.zip")
md5sums=('1a6d32b3014fdb91d27af64f735c5226')

build() {
  cd $srcdir/$pkgname-$pkgver

  sed -i -e '/^#.*NON_SYSTEM_ZLIB/d' \
         -e '/^#.*NON_SYSTEM_JPEG/d' \
         -e '/^#.*NON_SYSTEM_BZLIB/d' \
         include/IrrCompileConfig.h || return 1

  cd source/Irrlicht
  sed -i -e '/^CXXFLAGS/s:-g.*::' \
	 -e '/^CXXFLAGS/s:-Wall::' \
	 -e '/^CFLAGS/s/:= -O3 -fexpensive-optimizations/+=/' \
	 -e '/^CXXINCS/s:-Izlib -Ijpeglib::' \
	 -e '/^ZLIBOBJ/d' \
         -e '/^JPEGLIBOBJ/d' \
         -e '/^BZIP2OBJ/d' \
	 -e '/.o=.d/d' \
	 -e '/^sharedlib: LDFLAGS/s:+=:+= -lGL -lXxf86vm -ljpeg -lbz2 -lz:' \
         -e "/^INSTALL_DIR/s:=.*:=$pkgdir/usr/lib:" \
         -e 's/0-SVN/1/' \
         -e 's/.$(VERSION_MINOR) -o/ -o/' \
	 Makefile || return 1

  make sharedlib || return 1

  make || return 1

  install -d $pkgdir/usr/lib \
             $pkgdir/usr/share/licenses/$pkgname \
             $pkgdir/usr/share/$pkgname/examples/bin \
             $pkgdir/usr/share/doc/$pkgname

  make install || return 1

  cd $srcdir/$pkgname-$pkgver/
  install -m644 readme.txt $pkgdir/usr/share/licenses/$pkgname

  # Install static library and fix headers permissions
  install -m644 lib/Linux/libIrrlicht.a $pkgdir/usr/lib
  chmod 644 $pkgdir/usr/include/$pkgname/*

  # Install media files for examples
  cp -r media $pkgdir/usr/share/$pkgname

  # Install documentation
  cp -r doc/* $pkgdir/usr/share/doc/$pkgname
  rm -f $pkgdir/usr/share/doc/$pkgname/*.txt

  cd $pkgdir/usr/lib
  ln -s libIrrlicht.so.$pkgver libIrrlicht.so.1

  # Just a helper for examples compilation
  ln -s libIrrlicht.so.$pkgver $srcdir/$pkgname-$pkgver/lib/Linux/libIrrlicht.so

  # Edit, build and install the examples
  cd $srcdir/$pkgname-$pkgver/examples
  sed -i '/define USE_IRRKLANG/s:.*://&:' ./Demo/CDemo.h || return 1
  sed -i '/^CXXFLAGS/d' $(grep -Rl "^CXXFLAGS =" *)

  make || return 1

  install -m755  ../bin/Linux/* /$pkgdir/usr/share/$pkgname/examples/bin/
}
