# $Id: PKGBUILD 23342 2010-08-10 01:40:55Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=0.81.2.1
pkgrel=2
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis')
makedepends=('boost' 'cmake' 'zip' 'lzma-utils' 'p7zip')
source=(http://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${pkgver}_src.tar.lzma)
md5sums=('9ec91efdbcb6ee1cfda385958f2b6644')

build() {
    bsdtar -xf ${pkgname}_${pkgver}_src.tar.lzma

    cd spring_$pkgver

    cmake	. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DDATADIR=share/spring

    make
}

package() {
    cd spring_$pkgver

    make DESTDIR=$pkgdir install

    install -d $pkgdir/etc/spring
    echo '$HOME/.spring' > $pkgdir/etc/spring/datadir
}
