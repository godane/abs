# $Id: PKGBUILD 19555 2010-06-25 16:52:12Z heftig $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=ossp
pkgver=1.3.2
pkgrel=7
pkgdesc="Emulate OSS device using CUSE"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/osspd"
license=(GPL2)
depends=('fuse>=2.8.0')
makedepends=(pulseaudio alsa-lib)
optdepends=("pulseaudio: PulseAudio backend"
            "alsa-lib: ALSA backend")
backup=(etc/conf.d/osspd)
install=ossp.install
source=("http://downloads.sourceforge.net/project/osspd/${pkgname}-${pkgver}.tar.gz"
        osspd.rc.d osspd.conf.d)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Suppress warnings
  CFLAGS+=" -fno-strict-aliasing"

  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" prefix=/usr UDEVDIR=/lib/udev/rules.d install || return 1

  install -D -m644 README "$pkgdir/usr/share/doc/$pkgname/README"

  install -D -m755 "$srcdir/osspd.rc.d" "$pkgdir/etc/rc.d/osspd"
  install -D -m644 "$srcdir/osspd.conf.d" "$pkgdir/etc/conf.d/osspd"
}

md5sums=('fe0f9790fc85d5cd7883fc2179370da1'
         '69b745e0c499cd50019266a99082ce8b'
         '5a5a3db1bb3c98d48d4794613cf12ef0')
