# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Mikhail Vorozhtsov <mikhail.vorozhtsov@gmail.com>
pkgname=cross32-binutils
pkgver=2.20.1
pkgrel=3
_date=20100521
pkgdesc="A set of programs to assemble and manipulate binary and object files (for lib32)"
arch=('x86_64')
_target=i686-unknown-linux-gnu
url="http://www.gnu.org/software/binutils"
license=('GPL')
groups=('lib32')
depends=("binutils>=$pkgver-$pkgrel")
options=('!libtool')
source=(ftp://ftp.archlinux.org/other/binutils/binutils-${pkgver}_$_date.tar.bz2)
md5sums=('67053ffd8582e646e171fccd55b2f0f6')

build() {
  cd "$srcdir"

  rm -rf build
  mkdir build
  cd build

  ../configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --target=${_target} \
    --with-sysroot=/opt/lib32 \
    --disable-static \
    --disable-shared \
    --disable-multilib
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{lib,share}
}
