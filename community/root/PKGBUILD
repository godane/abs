# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sebastian Voecking <voeck@web.de>

pkgname=root
pkgver=5.26.00c
pkgrel=1
pkgdesc='C++ data analysis framework and interpreter from CERN.'
arch=('i686' 'x86_64')
url='http://root.cern.ch'
license=('LGPL2.1')
depends=('avahi' 'desktop-file-utils' 'ftgl' 'giflib' 'glew' 'graphviz' 'gsl' 'libldap' 'libmysqlclient'
         'libxft' 'postgresql-libs' 'python' 'unixodbc' 'shared-mime-info' 'xmlrpc-c' 'xorg-fonts-75dpi' 'mesa')
makedepends=('fftw')
install='root.install'
source=("ftp://root.cern.ch/root/root_v${pkgver}.source.tar.gz"
        'root.sh'
        'rootd'
        'root.desktop'
        'root.xml')
md5sums=('c7296c437623c931223c465864e71493'
         '0e883ad44f99da9bc7c23bc102800b62'
         'efd06bfa230cc2194b38e0c8939e72af'
         'ac61b17395d75a2705fefa2ef841a6bf'
         'e2cf69b204192b5889ceb5b4dedc66f7')

build() {
  cd ${pkgname}

  if [ ${CARCH} == 'i686' ]; then
    TARGET=linux;
  else
    TARGET=linuxx8664gcc;
  fi

  ./configure \
    ${TARGET} \
    --prefix=/usr \
    --disable-builtin-freetype \
    --disable-builtin-ftgl \
    --disable-builtin-pcre \
    --disable-builtin-zlib \
    --enable-gdml \
    --enable-gsl-shared \
    --enable-minuit2 \
    --enable-soversion

  make
}

package() {
  cd ${pkgname}

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/root.sh ${pkgdir}/etc/profile.d/root.sh
  install -D ${srcdir}/rootd ${pkgdir}/etc/rc.d/rootd
  install -D -m644 ${srcdir}/root.desktop ${pkgdir}/usr/share/applications/root.desktop
  install -D -m644 ${srcdir}/root.xml ${pkgdir}/usr/share/mime/packages/root.xml

  rm -rf ${pkgdir}/etc/root/daemons
}
