# $Id: PKGBUILD 21592 2010-07-16 19:40:26Z tdziedzic $
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=liboop
pkgver=1.0
pkgrel=3
pkgdesc='A low-level event loop management library'
arch=('i686' 'x86_64')
url='http://liboop.org'
options=('!libtool')
license=('LGPL')
depends=('glib2' 'readline' 'adns')
source=("http://download.ofb.net/${pkgname}/${pkgname}-$pkgver.tar.bz2")
md5sums=('88fc8decf99fd75d2af40e0a005fa0d3')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --without-tcl

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
