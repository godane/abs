# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=('boinc' 'boinc-nox')
pkgbase=boinc
pkgver=6.10.56
pkgrel=1
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
makedepends=('curl' 'libxslt' 'pkgconfig' 'perl-xml-sax' 'subversion' 'wxgtk')
options=('!libtool')
install=$pkgname.install
#changelog=$pkgname.changelog
source=($pkgname.rc $pkgname.bash $pkgname.desktop)
md5sums=('43605168e310f50cf426d2f9a7b39847'
         '05ed267db973ef7cbaf1118bb20bf9ce'
         '17969d849f3cf27c2100b20a7b7a7e64')

_svnroot="http://boinc.berkeley.edu/svn/tags"
_svnmod="boinc_core_release_${pkgver//./_}"

build() {
  cd ${srcdir}

  msg "Connecting to boinc.berkeley.edu SVN server..."
  svn co ${_svnroot}/${_svnmod} || (error "SVN checkout failed" && return 1)
  msg "SVN checkout done or server timeout"

  msg "Starting make..."
  if [ -d ${srcdir}/${_svnmod}-build ]; then
    rm -rf ${srcdir}/${_svnmod}-build
  fi
  cp -r ${srcdir}/$_svnmod ${srcdir}/$_svnmod-build
  cd ${srcdir}/$_svnmod-build

#configure
  ./_autosetup
  ./configure --prefix=/usr --disable-server --enable-unicode --with-ssl \
              --with-wxdir=/usr/lib --with-wx-config=$(which wx-config)
  make || return 1
}

package_boinc() {
  pkgdesc="Berkeley Open Infrastructure for Network Computing for desktop with X"
  depends=('curl' 'wxgtk')
  conflicts=('boinc-nox')

  cd ${srcdir}/$_svnmod-build

  make DESTDIR=${pkgdir} install || return 1

#install rc-script
  install -D -m755 ${srcdir}/$pkgname.rc ${pkgdir}/etc/rc.d/$pkgname || return 1

#install bash-completion
  install -D -m644 ${srcdir}/$pkgname.bash ${pkgdir}/etc/bash_completion.d/$pkgname || return 1

#install .desktop File
  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop || return 1

#install icons
  install -D -m644 ${srcdir}/${_svnmod}-build/clientgui/res/boincmgr.48x48.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png || return 1

#killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d || return 1
}

package_boinc-nox() {
  pkgdesc="Berkeley Open Infrastructure for Network Computing for server without X"
  depends=('curl')
  provides=('boinc')
  conflicts=('boinc')

  cd ${srcdir}/$_svnmod-build

  make DESTDIR=${pkgdir} install || return 1

#install rc-script
  install -D -m755 ${srcdir}/$pkgname.rc ${pkgdir}/etc/rc.d/$pkgname || return 1

#install bash-completion
  install -D -m644 ${srcdir}/$pkgname.bash ${pkgdir}/etc/bash_completion.d/$pkgname || return 1

#killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d || return 1

#removing graphical tools
  rm ${pkgdir}/usr/bin/boincmgr || return 1
  rm -rf ${pkgdir}/usr/share || return 1
}
