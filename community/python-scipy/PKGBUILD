#$Id: PKGBUILD 23618 2010-08-13 18:45:50Z angvp $
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-scipy
pkgver=0.8.0
pkgrel=3
pkgdesc="SciPy (pronounced 'Sigh Pie') is open-source software for mathematics, science, and engineering."
arch=('i686' 'x86_64')
url="http://www.scipy.org"
license=('BSD')
depends=('lapack' 'blas' 'python' 'python-numpy')
makedepends=('gcc' 'gcc-fortran')
conflicts=('scipy' 'scipy_core')
source=("http://downloads.sourceforge.net/scipy/scipy-${pkgver}.tar.gz")
md5sums=('f0bfc6141b90e6a31555b31486602251')

build() {
    unset LDFLAGS
    unset FFLAGS
    cd ${srcdir}/scipy-${pkgver}
    #Changing the arithmetic parameter (Thanks to Fabrizio Castellano)
    sed -i "s/\#define\ UNK\ 1/\#define\ IBMPC\ 1/" $srcdir/scipy-$pkgver/scipy/special/cephes/mconf.h
    python setup.py config_fc --fcompiler=gnu95 build || return 1
    python setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root=${pkgdir}
    install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
