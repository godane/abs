# Contributor: Firmicus <firmicus α gmx δ net>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=pdfedit
pkgver=0.4.4.1
pkgrel=2
pkgdesc="Full featured editor for manipulating PDF documents"
arch=('i686' 'x86_64')
url="http://pdfedit.petricek.net/index_e.html"
license=('GPL')
depends=('qt3' 't1lib' 'gsfonts')
makedepends=('boost' 'doxygen')
source=(http://downloads.sourceforge.net/pdfedit/${pkgname}-${pkgver}.tar.bz2
	'pdfedit.png' 'pdfedit.desktop')
md5sums=('43973c41c9264829107218c4b4021bbb'
         '70fd938b2ef9da748a093df7e20d1d7d'
         'c0572c2dd54065322321718aa49ed6b8')

build() {
	. /etc/profile.d/qt3.sh
	export PATH=$QTDIR/bin:$PATH
	export QMAKESPEC=$QTDIR/mkspecs/linux-g++

	cd ${srcdir}/${pkgname}-${pkgver}
	sed -i 's:/usr/share/fonts/type1/gsfonts:/usr/share/fonts/Type1:' src/xpdf/xpdf/GlobalParams.cc || return 1
	./configure --prefix=/usr --with-qmake=/opt/qt/bin/qmake
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make INSTALL_ROOT=${pkgdir} install

	install -D -m644 ../pdfedit.desktop ${pkgdir}/usr/share/applications/pdfedit.desktop
	install -D -m644 ../pdfedit.png ${pkgdir}/usr/share/pixmaps/pdfedit.png
}
