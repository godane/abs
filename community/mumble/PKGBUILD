# $Id: PKGBUILD 23350 2010-08-10 02:31:41Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Sebastian.Salich@gmx.de
# Contributor: Doc Angelo

# If you want support for your G15 Keyboard, please add 'g15daemon'
# to the depends and delete "no-g15" in the configure line below

pkgname=mumble
pkgver=1.2.2
pkgrel=8
arch=('i686' 'x86_64')
pkgdesc="A voice chat application similar to TeamSpeak"
license=('GPL')
depends=('qt' 'speex' 'celt-0.7' 'lsb-release' 'libxi' 'avahi' 'libsndfile' 'protobuf')
makedepends=('boost' 'mesa' 'avahi')
optdepends=('pulseaudio: for pulseaudio back-end' 'portaudio: for portaudio back-end' 'g15daemon: G15 Keyboard support')
conflicts=('mumble-client')
options=('!libtool') 
install=mumble.install
url="http://mumble.sourceforge.net/"
source=("http://downloads.sourceforge.net/mumble/$pkgname-$pkgver.tar.gz" \
	mumble11x.desktop )
md5sums=('de30ee85170e183b66568b53b04c5727'
         '70dcaa8ad2ecf68c6ba52ea03d112bcd')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Building mumble
  /usr/bin/qmake main.pro \
    CONFIG+="no-bundled-speex no-speechd no-bundled-celt no-g15 no-xevie no-server \
     no-embed-qt-translations packaged" \
    DEFINES+="PLUGIN_PATH=/usr/lib/mumble/plugins"
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  # create directories and copy files
  install -m755 -D ./release/mumble $pkgdir/usr/bin/mumble
  install -m755 -D ./release/mumble11x $pkgdir/usr/bin/mumble11x
  install -m755 -D ./scripts/mumble-overlay $pkgdir/usr/bin/mumble-overlay
  install -m755 -d $pkgdir/usr/lib/mumble/plugins
  install -m755 -D ./release/libmumble.so.$pkgver $pkgdir/usr/lib/mumble/
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib/mumble/libmumble.so
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib/mumble/libmumble.so.1
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib/mumble/libmumble.so.1.2
  install -m755 -D ./release/plugins/liblink.so $pkgdir/usr/lib/mumble/plugins/
  install -m755 -d $pkgdir/usr/share/applications
  install -m644 -D ./scripts/mumble.desktop $pkgdir/usr/share/applications/mumble.desktop
  install -m644 -D $srcdir/mumble11x.desktop $pkgdir/usr/share/applications/mumble11x.desktop
  install -m755 -d $pkgdir/usr/share/man/man1
  install -m644 -D ./man/mum* $pkgdir/usr/share/man/man1/
  install -m644 -D ./icons/mumble.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/mumble.svg
}
