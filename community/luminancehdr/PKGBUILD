# $Id: PKGBUILD 20649 2010-07-09 17:07:23Z tdziedzic $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=luminancehdr
pkgver=2.0.0
pkgrel=3
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('i686' 'x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('exiv2' 'fftw' 'gsl' 'hicolor-icon-theme' 'libtiff' 'openexr' 'qt')
replaces=('qtpfsgui')
install=${pkgname}.install
#changelog=$pkgname.changelog
source=("http://downloads.sourceforge.net/sourceforge/qtpfsgui/luminance-hdr_${pkgver}.tar.gz")
md5sums=('8ee07d3f43c13f8c0ce2e23dd02cefa4')

build() {
  cd luminance-hdr_${pkgver}

  if [ ${CARCH} = i686 ]; then
    sed -i 's/ -msse2//' project.pro
  fi

  lrelease project.pro

  qmake PREFIX=/usr

  make
}

package(){
  cd luminance-hdr_${pkgver}

  make INSTALL_ROOT=${pkgdir} install
}
