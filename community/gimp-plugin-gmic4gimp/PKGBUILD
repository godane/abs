# $Id: PKGBUILD 21062 2010-07-13 21:48:14Z heftig $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgname=gimp-plugin-gmic4gimp
pkgver=1.3.7.1
pkgrel=1
pkgdesc="Gimp plugin for the GMIC image procession framework. Large number of predefined filters"
depends=('fftw' 'gimp' 'graphicsmagick' 'ffmpeg')
arch=('i686' 'x86_64')
license=('custom:CeCILL')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_${pkgver}.tar.gz" \
	"build.patch")
url="http://gmic.sourceforge.net"

build() {
  cd "$srcdir/gmic-$pkgver"

  patch -Np1 -i "$srcdir/build.patch"

  ./configure --prefix=/usr

  cd src
  make gimp
}

package() {
  cd "$srcdir/gmic-$pkgver"
  install -D -m755 src/gmic_gimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

md5sums=('60159a643bbf6c4247974cf88db3eb5d'
         'a2ef070aec76544ddbfdab1de821a1fc')
