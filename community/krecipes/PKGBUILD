# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: shamrok <szamrok@gmail.com>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>

pkgname=krecipes
pkgver=2.0beta1
_pkgver=2.0-beta1
pkgrel=1
pkgdesc="A tool designed to make organizing your personal recipes collection fast and easy"
arch=('i686' 'x86_64')
url="http://extragear.kde.org/apps/krecipes/"
license=('GPL')
depends=('kdebase-runtime' 'qimageblitz' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4')
optdepends=('mysql' 'postgresql')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.gz)
md5sums=('db4be2380373262eca1ddbfbfccbcfb7')

build() { 
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
