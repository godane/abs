# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Jaroslaw Rosiek <philosoph@interia.pl>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=asciidoc
pkgver=8.5.3
pkgrel=4
pkgdesc="Text document format for short documents, articles, books and UNIX man pages"
arch=('any')
url="http://www.methods.co.nz/asciidoc/"
license=('GPL')
depends=('python' 'libxslt' 'docbook-xsl')
optdepends=('lilypond: music-filter'
	    'imagemagick: music-filter (used in conjunction with lilypond)'
	    'source-highlight: source-highlight-filter'
	    'fop: pdf generation'
	    'lynx: text generation'
	    'w3m: text generation (alternative to lynx)')
source=(http://downloads.sourceforge.net/asciidoc/asciidoc-${pkgver}.tar.gz)
md5sums=('7377872275c2a575151655b19ac6dd3e')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/usr --sysconfdir=/etc
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make install DESTDIR=${pkgdir}

	install -Dm644 asciidocapi.py \
	  ${pkgdir}/usr/lib/python2.6/site-packages/asciidocapi.py

	install -Dm644 vim/syntax/asciidoc.vim \
	  ${pkgdir}/usr/share/vim/vimfiles/syntax/asciidoc.vim

	install -Dm644 vim/ftdetect/asciidoc_filetype.vim \
	  ${pkgdir}/usr/share/vim/vimfiles/ftdetect/asciidoc.vim
}
