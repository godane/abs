# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=filezilla
pkgver=3.3.3
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('i686' 'x86_64')
url="http://filezilla-project.org/"
license=('GPL')
depends=('dbus-core' 'xdg-utils' 'wxgtk' 'libidn' 'hicolor-icon-theme')
install=filezilla.install
source=(http://downloads.sourceforge.net/project/filezilla/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_src.tar.bz2)
md5sums=('e98949facf23299ff791e046929852a7')

build() {
	cd ${srcdir}/filezilla-${pkgver}

	./configure --prefix=/usr --disable-manualupdatecheck || return 1
	make || return 1
}

package() {
	cd ${srcdir}/filezilla-${pkgver}

	make DESTDIR=${pkgdir} install || return 1
}
