# $Id: PKGBUILD 23672 2010-08-14 15:33:36Z lcarlier $
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>

pkgbase=sauerbraten
pkgname=('sauerbraten' 'sauerbraten-data')
pkgver=2010_07_28_justice_edition
pkgrel=1
pkgdesc="Improved version of the Cube engine"
arch=('i686' 'x86_64')
url="http://www.sauerbraten.org/"
license=('ZLIB')
depends=('sdl_mixer' 'sdl_image' 'libgl')
makedepends=('mesa')
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}_${pkgver}_linux.tar.bz2
	sauerbraten-client
	sauerbraten-server
	sauerbraten.confd
	sauerbraten.desktop
	sauerbraten.png
	license.txt)
md5sums=('0947cb37b628873063270be1fcce27f6'
         'ac66ef9e80d6fc1b4b047df1c36c07c0'
         'd5b6d9be5a5337f78d1d1752976b740e'
         '937c1434956425b912e29b1f2fff7eef'
         '5b528424ed46e05124420975d99370e5'
         '3e602f421f7342cf8c3ed16314523d6f'
         'cf46cd0148f03b7477f7c54aec865abe')

build() {
  cd ${srcdir}/$pkgname/src

  make

  # remove cvs directories
  find ${srcdir}/$pkgname -type d -name CVS -exec rm -rf '{}' \; 2> /dev/null
}

package_sauerbraten() {
  depends=('sdl_mixer' 'sdl_image' 'libgl' 'sauerbraten-data')

  cd ${srcdir}/$pkgname/src

  make DESTDIR="${pkgdir}" install

  cd ..
  install -D bin_unix/native_client ${pkgdir}/opt/sauerbraten/bin/linux_client
  install -D bin_unix/native_server ${pkgdir}/opt/sauerbraten/bin/linux_server

  install -Dm755 ${srcdir}/sauerbraten-client ${pkgdir}/usr/bin/sauerbraten-client
  install -Dm755 ${srcdir}/sauerbraten-server ${pkgdir}/usr/bin/sauerbraten-server
  install -Dm644 ${srcdir}/sauerbraten.confd ${pkgdir}/etc/conf.d/sauerbraten
  install -Dm644 ${srcdir}/sauerbraten.desktop ${pkgdir}/usr/share/applications/sauerbraten.desktop
  install -Dm644 ${srcdir}/sauerbraten.png ${pkgdir}/usr/share/pixmaps/sauerbraten.png
  install -Dm644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/sauerbraten/license.txt
}

package_sauerbraten-data() {
  pkgdesc="Improved version of the Cube engine - datas files"
  arch=('any')
  depends=()

  cd ${srcdir}/sauerbraten

  install -d ${pkgdir}/opt/sauerbraten
  cp -r data ${pkgdir}/opt/sauerbraten
  cp -r packages ${pkgdir}/opt/sauerbraten

  # make namcap happy
  chmod -R go+rx ${pkgdir}/opt/sauerbraten/packages/argh

  install -Dm644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}-data/license.txt
}
