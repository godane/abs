# $Id: PKGBUILD 22916 2010-08-01 07:34:43Z ibiru $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Alexandru Mizeranschi <lexu_85@yahoo.com>
# Contributor: Conor Callahan <majikstreet@gmail.com>

pkgname=gyachi
pkgver=1.2.10
pkgrel=1
pkgdesc="An improved fork of the GyachE Yahoo! client"
arch=('i686' 'x86_64')
url="http://gyachi.sourceforge.net/"
license=('GPL')
depends=('jasper' 'libgtkhtml' 'libmcrypt' 'libnotify' 'libv4l' 'gpgme'
         'gtkspell')
makedepends=('pkgconfig' 'alsa-lib' 'pulseaudio' 'imagemagick')
optdepends=('pulseaudio: for pulseaudio support')
options=('!libtool')
source=(http://downloads.sourceforge.net/gyachi/gyachi-${pkgver}.tar.gz)
md5sums=('68625f81c7be8cc6d67599fdb02f0178')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    export CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64"
    ./autogen.sh || return 1
    ./configure --prefix=/usr --libexecdir=/usr/lib/gyachi \
      --disable-rpath \
      --disable-wine \
      --disable-gtktest \
      --disable-glibtest \
      --with-x || return 1
    make || return 1
}

package () {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install || return 1
    #icon
    install -Dm644 "${pkgdir}/usr/share/gyachi/themes/gyachi-classic/gyach-icon_48.png" \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
