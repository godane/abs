# $Id: PKGBUILD 23332 2010-08-10 01:00:21Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=openarena
pkgver=0.8.5
_oldver=0.8.1
pkgrel=2
pkgdesc="A violent, sexy, multiplayer first person shooter based on the ioquake3 engine"
arch=('i686' 'x86_64')
url="http://openarena.ws/"
license=('GPL')
depends=('sdl' 'libvorbis' 'curl' 'openarena-data')
makedepends=('openal' 'mesa')
source=(http://download.tuxfamily.org/$pkgname/rel/081/oa081.zip
        http://download.tuxfamily.org/$pkgname/rel/085/oa085p.zip
        $url/svn/source/081/$pkgname-engine-$_oldver-1.tar.bz2
        $pkgname.png
        $pkgname-server.png
        $pkgname.desktop
        $pkgname-server.desktop)
md5sums=('49006bcb02b4e8ea3d06749e8f4e4887'
         'b2a0437da751cd50dd2351ed9e0c4e9d'
         '4ee696eacc4b0350f9dbb5588dcd74a2'
         'c69f824dc500d436fc631b78b6a62dc8'
         '17e2f4fa060ed97ff6fe1eaff647bcfb'
         '229c87648c10e48b523c25d24aba8372'
         '00649517a4dd08cd9c6e99d6703f01f1')

build() {
    # uncomment to disable semi-nude models:
    # 	rm -f $pkgname-$_oldver/baseoa/pak2-players-mature.pk3
    # (resulting version won't work with most servers!!)

    cd $srcdir/$pkgname-engine-$_oldver

    make DEFAULT_BASEDIR=/usr/share/$pkgname \
        BUILD_CLIENT_SMP=0 \
        USE_LOCAL_HEADERS=0 \
        GENERATE_DEPENDENCIES=0 \
        OPTIMIZE=
}

package() {
    cd $srcdir/$pkgname-engine-$_oldver

    install -d $pkgdir/usr/{bin,share/{$pkgname,applications,pixmaps}}

    make COPYDIR=$pkgdir/usr/share/$pkgname copyfiles

    [ "$CARCH" = "x86_64" ] && {
        mv -f $pkgdir/usr/share/$pkgname/$pkgname.$CARCH $pkgdir/usr/bin/$pkgname
        mv -f $pkgdir/usr/share/$pkgname/oa_ded.$CARCH $pkgdir/usr/bin/$pkgname-server
    } || {
        mv -f $pkgdir/usr/share/$pkgname/$pkgname.i386 $pkgdir/usr/bin/$pkgname
        mv -f $pkgdir/usr/share/$pkgname/oa_ded.i386 $pkgdir/usr/bin/$pkgname-server
    }

    find $pkgdir/usr/share -type f -exec chmod 644 {} +

    install -m 644 $srcdir/$pkgname{,-server}.png $pkgdir/usr/share/pixmaps/
    install -m 644 $srcdir/$pkgname{,-server}.desktop $pkgdir/usr/share/applications/
}

# vim: sw=2:ts=2 et:
