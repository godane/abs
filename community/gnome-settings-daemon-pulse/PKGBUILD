# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=gnome-settings-daemon
pkgname=$_pkgbasename-pulse
pkgver=2.30.2
pkgrel=1
pkgdesc="The GNOME Settings daemon with PulseAudio support"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd>=2.30.1' 'gnome-desktop>=2.30.2' 'libnotify>=0.4.5'
         'libxxf86misc' 'pulseaudio' 'libcanberra-pulse' 'hicolor-icon-theme')
makedepends=('intltool' 'pkg-config')
conflicts=('gnome-control-center<2.22.0' "$_pkgbasename")
provides=("$_pkgbasename=$pkgver-$pkgrel")
options=(!emptydirs !libtool)
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=(pulseaudio-gnome)
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.30/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('7cd22dab52f52900ee13a5bc0c1a186e91d33e77f5333c4622f84d85e4eb71f4')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/bin --disable-static

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgbasename}.schemas" --domain gnome-settings-daemon ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
