# $Id: PKGBUILD 23199 2010-08-08 02:23:19Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl> 
# Contributor: sausageandeggs <s_stoakley at hotmail.co.uk>

pkgname=singularity
pkgver=0.30b
pkgrel=1
pkgdesc="Strategy game - simulation of true AI"
arch=('any')
url="http://emhsoft.com/singularity/index.html"
license=('GPL2' 'custom')
depends=('python' 'python-pygame' 'sdl_image' 'sdl_mixer' 'python-numpy')
source=("http://endgame-singularity.googlecode.com/files/$pkgname-$pkgver-src.tar.gz" \
        "http://endgame-singularity.googlecode.com/files/endgame-$pkgname-music-006.zip" \
        "singularity")
md5sums=('4e59703a7f8a0d0a06984f41160f7735'
         'f16d550add4df9c41d4fd68d8ebff40c'
         'a65ac552caefaf8d41790ca94b9ffb82')

build() {
  cd "$srcdir/$pkgname-$pkgver"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install game
  install -Dm644 singularity.py $pkgdir/usr/share/singularity/singularity.py
  cp -R code data "$srcdir/endgame-$pkgname-music-006/music" "$pkgdir/usr/share/$pkgname"

  # install shell script
  install -Dm755 "$srcdir/singularity" "$pkgdir/usr/bin/singularity"

  # license
  # code is GPL2, but data & music fall under other licenses
  install -Dm644 README.txt "$pkgdir/usr/share/doc/$pkgname/README.txt"
  install -d "$pkgdir/usr/share/licenses/$pkgname/"
  cd "$pkgdir/usr/share/licenses/$pkgname/"
  ln -s ../../doc/$pkgname/README.txt .
}
# vim: sw=2:ts=2 et:
