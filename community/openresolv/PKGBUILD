# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>

pkgname=openresolv
pkgver=3.3.4
pkgrel=3
pkgdesc="resolv.conf management framework (resolvconf)"
arch=(any)
url="http://roy.marples.name/projects/openresolv"
license=('custom:BSD')
backup=(etc/resolvconf.conf)
provides=(resolvconf)
source=("http://roy.marples.name/downloads/openresolv/${pkgname}-${pkgver}.tar.bz2"
        "daemon-status.patch")

package() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/daemon-status.patch"

  make DESTDIR="$pkgdir" PREFIX=/usr SYSCONFDIR=/etc LIBEXECDIR="/usr/lib/${pkgname}" install

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '2,25{s:^# \?::;p}' resolvconf.in > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

md5sums=('a29f39cdbd3c404d4e50dd1e00a0c27c'
         '5f38aacbff500ac14f4d228623b52e7b')
