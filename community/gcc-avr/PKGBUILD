# $Id$
# Maintainer :
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=gcc-avr
pkgver=4.5.0
pkgrel=1
_snapshot=4.5-20100610
pkgdesc="The GNU avr Compiler Collection"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'custom')
url="http://gcc.gnu.org/"
depends=('binutils-avr>=2.20.1' 'cloog-ppl' 'gcc-libs>=4.5.0' 'libmpc' 'libelf')
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip')
#source=(http://ftp.gnu.org/gnu/gcc/${pkgname/-avr}-${pkgver}/gcc-{core,g++}-${pkgver}.tar.bz2)
source=(ftp://gcc.gnu.org/pub/gcc/snapshots/${_snapshot}/gcc-{core,g++}-${_snapshot}.tar.bz2)
md5sums=('d0c6573f18e61d0a6c6fe742066346cd'
         '4c5369d27b8cb4c4e447553f064d0f16')

build() {
  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

#  cd ${srcdir}/${pkgname/-avr}-${pkgver}
  cd ${srcdir}/${pkgname/-avr}-${_snapshot}

  mkdir build
  cd build
  ../configure --disable-libssp \
               --disable-nls \
               --enable-languages=c,c++ \
               --infodir=/usr/share/info \
               --libdir=/usr/lib \
               --libexecdir=/usr/lib \
               --mandir=/usr/share/man \
               --prefix=/usr \
               --target=avr \
               --with-gnu-as \
               --with-gnu-ld \
               --with-as=/usr/bin/avr-as \
               --with-ld=/usr/bin/avr-ld

  make || return 1
}

package() {
#  cd ${srcdir}/${pkgname/-avr}-${pkgver}
  cd ${srcdir}/${pkgname/-avr}-${_snapshot}

  cd build
  make -j1 DESTDIR=${pkgdir} install || return 1

  rm -f ${pkgdir}/usr/lib/libiberty.a || return 1
  rm -rf ${pkgdir}/usr/share/man/man7 || return 1
  rm -rf ${pkgdir}/usr/share/info || return 1
}

# vim:set ts=2 sw=2 et:
