# $Id: PKGBUILD 21419 2010-07-16 12:02:09Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sekenre <sekenre at ukfsn dot org>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>

pkgname=metamail
pkgver=2.7
pkgrel=5
pkgdesc="Used to display and process MIME messages"
arch=('i686' 'x86_64')
url="http://packages.debian.org/stable/mail/metamail"
license=('custom')
depends=('ncurses')
options=('!libtool')
noextract=(metamail_$pkgver-52.diff.gz)
source=("http://ftp.de.debian.org/debian/pool/main/m/metamail/metamail_$pkgver.orig.tar.gz" \
	"http://ftp.de.debian.org/debian/pool/main/m/metamail/metamail_$pkgver-54.diff.gz" \
	license)
md5sums=('c6967e9bc5d3c919764b02df24efca01'
         '2071dc7b9c33345443ab9a619e640a69'
         '38be502570f3ce5b5a2bafd6a876373f')

build() {
  cd $srcdir/metamail-$pkgver.orig

  zcat $srcdir/metamail_$pkgver-54.diff.gz | patch -p1
  sed -i 's|getline|xgetline|' metamail/uue.c src/metamail/uue.c

  sh ./bootstrap
  sh ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  install -D -m0644 $srcdir/license $pkgdir/usr/share/licenses/$pkgname/license
}
