# $Id$
# Maintainer:
# Contributor: damir <damir@archlinux.org>

pkgname=x11vnc
pkgver=0.9.10
pkgrel=1
pkgdesc="A VNC server for real X displays"
arch=("i686" "x86_64")
url="http://www.karlrunge.com/x11vnc/"
license=('GPL2')
depends=('openssl' 'libjpeg' 'zlib' 'libx11' 'libxtst' 'libxinerama' 'libxdamage' 'libxrandr' 'avahi')
optdepends=('tk: for GUI support')
backup=('etc/conf.d/x11vnc')
source=("http://downloads.sourceforge.net/libvncserver/${pkgname}-${pkgver}.tar.gz"
	'x11vnc.rc'
	'x11vnc.conf')
md5sums=('691902f477bdcaa7ee77cc4ecc43a639' '93fc00459cfa5fe76f07045ee89bd6d4'\
         '3a6df4b01c7f02741904b978bb7bcd90')
sha1sums=('ec7082ee48f1aca0597208bc29197d56a2ff3bff' '3abb3fcadff32662ab847a9e772cd23cfdfe3d1a'\
         '11011cce06511e9ba61672a1c80bb63a3a187539')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m 755 "${srcdir}/x11vnc.rc" "${pkgdir}/etc/rc.d/x11vnc"
  install -D -m 644 "${srcdir}/x11vnc.conf" "${pkgdir}/etc/conf.d/x11vnc"
}
