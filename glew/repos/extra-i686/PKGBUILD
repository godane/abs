# $Id$
# Maintainer:
# Contributor: SleepyDog

pkgname=glew
pkgver=1.5.5
pkgrel=1
pkgdesc="A cross-platform C/C++ extension loading library"
arch=('i686' 'x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu' 'libxi' 'mesa')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tgz)
md5sums=('3621f27cfd3e33d5dbcc1111ecb5b762')
sha1sums=('b6f28142b77a99e719b113b6859ae120f131cc91')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|lib64|lib|' config/Makefile.linux
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GLEW_DEST="${pkgdir}/usr" install
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
